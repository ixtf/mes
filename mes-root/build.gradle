plugins {
    id "io.spring.dependency-management" version "1.0.9.RELEASE"
    id "io.franzbecker.gradle-lombok" version "3.3.0"
    id "com.palantir.docker" version "0.25.0"
    id "com.github.johnrengelman.shadow" version "5.2.0"
    id "com.google.protobuf" version '0.8.11' apply false
    id "io.swagger.core.v3.swagger-gradle-plugin" version "2.1.1" apply false
}
allprojects {
    group 'com.hengyi.japp.mes'
    version '3.0'

    apply plugin: 'idea'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: "io.spring.dependency-management"
    apply plugin: 'io.franzbecker.gradle-lombok'

    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        maven {
            url 'https://jitpack.io'
        }
    }

    dependencyManagement {
        imports {
            mavenBom "com.github.ixtf:bom:${bomVersion}"
        }

        dependencies {
            dependency "com.google.protobuf:protobuf-java:${protobufVersion}"
            dependency "com.google.protobuf:protoc:${protobufVersion}"
            dependency "io.rsocket:rsocket-core:${rsocketVersion}"
            dependency "io.rsocket:rsocket-transport-netty:${rsocketVersion}"
            dependency "io.rsocket.rpc:rsocket-rpc-protobuf:${rsocketRpcVersion}"
            dependency "io.rsocket.rpc:rsocket-rpc-core:${rsocketRpcVersion}"
        }
    }
}
subprojects {
    apply plugin: 'java'
    sourceCompatibility = 11
    targetCompatibility = 11
    apply plugin: 'application'
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'com.palantir.docker'

    dependencies {
        testCompile "junit:junit",
                "io.vertx:vertx-unit",
                "io.vertx:vertx-web-client",
                "org.apache.lucene:lucene-demo"
    }

    /**
     * 打包 docker 准备
     */
    docker {
        dependsOn shadowJar
        name "hengyi/${project.name}:${project.version}"
        buildArgs([BUILD_VERSION: "${project.version}"])
        files tasks.shadowJar.outputs
        noCache true
    }
    shadowJar {
        archiveName "${project.name}-fat.jar"
    }
    jar.manifest {
        attributes(
                'Implementation-Title': name,
                'Implementation-Version': version,
                'Built-By': System.getProperty('user.name'),
                'Built-JDK': System.getProperty('java.version'),
                'Source-Compatibility': sourceCompatibility,
                'Target-Compatibility': targetCompatibility
        )
    }
//    javadoc.options {
//        encoding = 'UTF-8'
//        use = true
//        links 'https://docs.oracle.com/javase/8/docs/api/'
//    }
//    task javadocJar(type: Jar) {
//        classifier = 'javadoc'
//        from javadoc
//    }
//    task sourcesJar(type: Jar) {
//        classifier = 'sources'
//        from sourceSets.main.allSource
//    }
//    artifacts {
//        archives javadocJar, sourcesJar
//    }
}

